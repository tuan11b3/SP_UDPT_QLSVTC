USE [QLSVTC]
GO
/****** Object:  StoredProcedure [dbo].[SP_DS_LOPTINCHI_MODK]    Script Date: 12/14/2024 10:36:08 AM ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [dbo].[SP_DS_LOPTINCHI_MODK]
AS
DECLARE @ngayht DATE = CAST(GETDATE() AS DATE)
DECLARE @ngaymodk DATE
DECLARE @ngaydodk DATE
DECLARE @mankhk INT = NULL;
SET XACT_ABORT ON;
BEGIN
	SELECT @mankhk=MANKHK FROM NKHK WHERE NGAYMODK <= @ngayht AND @ngayht < NGAYDONGDK;
	IF @mankhk IS NULL
	BEGIN
		RAISERROR(N'Ngoài thời gian đăng ký!', 16, 1)
		RETURN
	END
	SELECT  FilteredLTC.MALTC,
			mh.MAMH,
			mh.TENMONHOC,
			FilteredLTC.NHOM,
			STUFF(
			(SELECT '<br>' + gv.HO + ' ' + gv.TEN
			 FROM GIANGVIEN gv
			 INNER JOIN GIANGDAY gd ON gv.MAGV = gd.MAGV
			 WHERE gd.MALTC = FilteredLTC.MALTC
			 FOR XML PATH(''), TYPE).value('.', 'NVARCHAR(MAX)'), 1, 4, '') AS 'HOTENGV',
			COUNT(dk.MASV) AS sosvdk,
			STUFF((
				SELECT '<br>' + N'thứ: ' + CAST(tb.THU AS NVARCHAR)+ ' -----> ' + N'buổi: ' + CAST(tb.BUOI AS NVARCHAR)
				FROM THUBUOI tb
				INNER JOIN GIANGDAY gd ON tb.MATHUBUOI = gd.MATB
				WHERE gd.MALTC = FilteredLTC.MALTC
				FOR XML PATH(''), TYPE).value('.', 'NVARCHAR(MAX)'), 1, 4, '') AS 'THOIKHOABIEU'
	FROM (SELECT MALTC, NHOM, MAMH FROM LOPTINCHI WHERE MANKHK = @mankhk AND HUYLOP = 0) AS FilteredLTC
	INNER JOIN MONHOC mh ON FilteredLTC.MAMH = mh.MAMH
	LEFT JOIN DANGKY dk ON FilteredLTC.MALTC = dk.MALTC AND dk.HUYDANGKY=0
	GROUP BY  FilteredLTC.MALTC,
			  FilteredLTC.NHOM,
			  mh.TENMONHOC,
			  mh.MAMH 
	ORDER BY  mh.TENMONHOC,
			  FilteredLTC.NHOM;
END