USE [QLSVTC]
GO
/****** Object:  StoredProcedure [dbo].[SP_DANGKY_LOPTINCHI]    Script Date: 12/15/2024 11:11:41 AM ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [dbo].[SP_DANGKY_LOPTINCHI]
@MASV NCHAR(10),
@MALTC NCHAR(10)
AS
DECLARE @mankhk INT;
DECLARE @TKB_LTC TABLE (MATB INT);
DECLARE @TKB_SV TABLE (MATB INT)
SET XACT_ABORT ON;
BEGIN
	SELECT @mankhk=MANKHK FROM LOPTINCHI WHERE MALTC=@MALTC
	INSERT INTO @TKB_LTC
	SELECT MATB FROM GIANGDAY g WHERE g.MALTC = @MALTC
	INSERT INTO @TKB_SV (MATB)
	SELECT MATB 
	FROM (SELECT MALTC FROM DANGKY WHERE MASV = @MASV AND HUYDANGKY=0) FilteredDANGKY
	INNER JOIN LOPTINCHI ltc ON FilteredDANGKY.MALTC = ltc.MALTC AND MANKHK = @mankhk
	INNER JOIN GIANGDAY gd ON ltc.MALTC = gd.MALTC

	IF NOT EXISTS(SELECT 1 FROM @TKB_LTC a WHERE a.MATB IN (SELECT MATB FROM @TKB_SV))
	BEGIN
		IF EXISTS(SELECT 1 FROM DANGKY WHERE MASV = @MASV AND MALTC = @MALTC)
		BEGIN
			UPDATE DANGKY 
			SET HUYDANGKY = 0
			WHERE MASV = @MASV AND MALTC=@MALTC
			PRINT N'Hủy đăng ký ltc của SV được cập nhật = False!'
		END
		ELSE 
		BEGIN
			INSERT INTO DANGKY(MALTC, MASV) VALUES (@MALTC, @MASV) -- dangky
			PRINT N'SV đăng ký LTC mới thành công!'
		END
	END
		ELSE
			RAISERROR(N'Trùng lịch học',16, 1)
END